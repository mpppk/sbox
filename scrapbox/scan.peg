package scrapbox

type Parser Peg {
    s Scan         // parserが自動生成するフィールド変数と区別するために
                   //   敢えて埋め込みを行っていない。
}

root <- expression EOT /
        expression <.+> {p.s.Err(begin)} EOT /
        <.+> {p.s.Err(begin)} EOT

EOT <- !.

expression <- literal+

literal <- bracketText /
<[^\[\n]+> {
    p.s.Push(&PlainText{Text: text})
} /
<[^\]\n]+> {
    p.s.Push(&PlainText{Text: text})
} /
'\n' {
    p.s.Push(NewNewLineText())
}

bracketText <- '[' innerDecoratedText ']' /
<'[' plainTextLine> {
    p.s.Push(&PlainText{Text: text})
}

innerDecoratedText <- '* ' <plainTextLine> {
    p.s.Push(NewBoldText(text))
} /
'/ ' <plainTextLine> {
    p.s.Push(NewItalicText(text))
} /
'- ' <plainTextLine> {
    p.s.Push(NewStrikeThroughText(text))
} /
<plainTextLine> {
    p.s.PushLinkFromRawText(text)
}

plainTextLine <- [^\]\[\n]+